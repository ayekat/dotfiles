# $Id: PKGBUILD 60970 2011-12-19 21:33:58Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>
# ayekat flavoured

pkgname=dwm
pkgver=6.0
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
sources=(
	$pkgname-$pkgver.tar.gz
	config.h
)
patches=(
	01-dwm-6.0-bstack.diff
	02-dwm-6.0-push.diff
	03-dwm-6.0-pertag.diff
	04-dwm-6.0-attachbelow.diff
	05-dwm-6.0-pango.diff
	06-dwm-6.0-scratchpad.diff
	07-dwm-5.8.2-fibonacci.diff
	10-dwm-6.0-taags.diff
	11-dwm-6.0-startup.diff
)
patches_disabled=(
	08-dwm-6.0-trivial_single_tagset.diff
	09-dwm-6.0-smfact.diff
)
source=(${sources[@]} ${patches[@]})

build() {
	# enable for a full rebuild with patching (need to remove src directory):
	#rebuild=1
	cd $srcdir/$pkgname-$pkgver
	cp $srcdir/config.h .
	if [ $rebuild ]; then
		for PATCH in "${patches[@]}"; do
			printf "\e[1mpatching %s ...\e[0m\n" "$PATCH"
			patch -p1 -i "$srcdir"/"$PATCH"
		done
		sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
		sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
		sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
		sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
		sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk
		echo "CFLAGS += -DHOST_$HOSTNAME" >> config.mk
	fi
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
}
# no md5sums(); run 'makepkg -g >> PKGBUILD' to generate md5sums()
