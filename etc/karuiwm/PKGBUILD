# Maintainer: ayekat <takeya@bluewin.ch>

# NOTE:
# This PKGBUILD will require you to provide your config.h.
# An example config.h can be found here:
# https://gitlab.com/ayekat/karuiwm/blob/legacy/config.def.h

pkgname=karuiwm-legacy-ayekat-git
_pkgname=karuiwm
pkgdesc="Dynamically tiling window manager for X11 (legacy version, ayekat's config)"
pkgver=r204.55f3a56
pkgrel=2
arch=(x86_64)
url='https://gitlab.com/ayekat/karuiwm'
license=(MIT)
depends=(libx11 libxinerama)
makedepends=(inetutils)
provides=(karuiwm karuiwm-legacy karuiwm-legacy-ayekat)
conflicts=(karuiwm karuiwm-legacy karuiwm-legacy-ayekat)
optdepends=('wmname: Fix Java client behaviour for non-reparenting WM')
source=('git+https://gitlab.com/ayekat/karuiwm.git' config.h)
md5sums=(SKIP SKIP)

pkgver() {
  cd "$_pkgname"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir"/"$_pkgname"
  git checkout legacy
  cp "$srcdir"/config.h config.h
  sed -i 's/karuiwm/karuiwm-legacy/g' "$srcdir"/"$_pkgname"/karuiwm.c
}

build() {
  cd "$srcdir"/"$_pkgname"
  make
}

package() {
  cd "$srcdir/$_pkgname"
  depends+=(ayekat-utils-deps-desktop dmenu mpc rxvt-unicode sakura)
  install -Dm 755 karuiwm "$pkgdir"/usr/bin/karuiwm-legacy
}
